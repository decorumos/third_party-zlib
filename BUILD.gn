# Copyright (c) 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("zlib_config") {
  include_dirs = [ "." ]
}

static_library("zlib_x86_simd") {
  if (current_cpu == "x86" || current_cpu == "x64") {
    sources = [
      "crc_folding.c",
      "fill_window_sse.c",
    ]
    cflags = [
      "-msse4.2",
      "-mpclmul",
    ]
  } else {
    sources = [
      "simd_stub.c",
    ]
  }
}

config("zlib_warnings") {
  visibility = [ ":*" ]

  cflags = [
    "-Wno-implicit-function-declaration",
    "-Wno-shift-negative-value",
  ]

  if (is_clang && (current_cpu == "x86" || current_cpu == "x64")) {
    cflags += [ "-Wno-incompatible-pointer-types" ]
  }
}

static_library("zlib") {
  sources = [
    "adler32.c",
    "compress.c",
    "crc32.c",
    "crc32.h",
    "deflate.c",
    "deflate.h",
    "gzclose.c",
    "gzguts.h",
    "gzlib.c",
    "gzread.c",
    "gzwrite.c",
    "infback.c",
    "inffast.c",
    "inffast.h",
    "inffixed.h",
    "inflate.c",
    "inflate.h",
    "inftrees.c",
    "inftrees.h",
    "mozzconf.h",
    "trees.c",
    "trees.h",
    "uncompr.c",
    "x86.h",
    "zconf.h",
    "zlib.h",
    "zutil.c",
    "zutil.h",
  ]

  if (current_cpu == "x86" || current_cpu == "x64") {
    sources += [ "x86.c" ]
  }

  configs += [ ":zlib_warnings" ]
  public_configs = [ ":zlib_config" ]
  deps = [ ":zlib_x86_simd" ]
}

config("minizip_warnings") {
  visibility = [ ":*" ]

  cflags = [
    "-Wno-implicit-function-declaration",
  ]

  if (is_clang) {
    # zlib uses `if ((a == b))` for some reason.
    cflags += [ "-Wno-parentheses-equality" ]
  }
}

static_library("minizip") {
  sources = [
    "contrib/minizip/ioapi.c",
    "contrib/minizip/ioapi.h",
    "contrib/minizip/unzip.c",
    "contrib/minizip/unzip.h",
    "contrib/minizip/zip.c",
    "contrib/minizip/zip.h",
  ]

  deps = [ ":zlib" ]
  configs += [ ":minizip_warnings" ]
  public_configs = [ ":zlib_config" ]
}
